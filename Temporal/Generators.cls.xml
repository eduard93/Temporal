<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="Temporal.Generators">
<Description>
Class, all methods for class
propertyGet, propertyGetStatic, propertySet methods for property</Description>
<Abstract>1</Abstract>
<TimeCreated>64306,75925.447893</TimeCreated>

<Method name="generateAllMethods">
<ClassMethod>1</ClassMethod>
<FormalSpec>class:%Dictionary.ClassDefinition</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	set sc = ..generateProperiesMethods(class) 
	quit:$$$ISERR(sc) sc
	
	set sc = ..generateClassMethods(class) 
	quit:$$$ISERR(sc) sc
	
	quit class.%Save()
]]></Implementation>
</Method>

<Method name="generateProperiesMethods">
<ClassMethod>1</ClassMethod>
<FormalSpec>class:%Dictionary.ClassDefinition</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#dim sc As %Status = $$$OK
	for i=1:1:class.Properties.Count() {
		#dim property As %Dictionary.PropertyDefinition
		set property = class.Properties.GetAt(i)
		CONTINUE:property.Parameters.GetAt("TIMEPROPERTY")'=$$$YES // Property is not a time roperty
		set sc = ..generatePropertyMethods(class, property)
		quit:$$$ISERR(sc)
	}
	quit sc
]]></Implementation>
</Method>

<Method name="generatePropertyMethods">
<ClassMethod>1</ClassMethod>
<FormalSpec>class:%Dictionary.ClassDefinition,property:%Dictionary.PropertyDefinition</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#dim sc As %Status = $$$OK
	
	set sc = ..generatePropertyGet(class, property) 
	quit:$$$ISERR(sc) sc
	
	set sc = ..generatePropertyGetStatic(class, property) 
	quit:$$$ISERR(sc) sc
	
	set sc = ..generatePropertySet(class, property) 
	quit:$$$ISERR(sc) sc
	
	set sc = ..setPropertyObject(class, property) 
	quit sc
]]></Implementation>
</Method>

<Method name="generatePropertyGet">
<ClassMethod>1</ClassMethod>
<FormalSpec>class:%Dictionary.ClassDefinition,property:%Dictionary.PropertyDefinition</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#dim sc As %Status = $$$OK
	
	#dim method As %Dictionary.MethodDefinition = ..getPropertyMethod(class, property, "Get")
	
	set method.ClassMethod = $$$NO
	set method.Description = property.Name _ " getter method in object context"
	set method.ReturnType = property.Type
	
	set code = ##class(Temporal.GeneratorsProperty).getCode("Get", property.Name, ..getGlobal(class, property))
	do method.Implementation.Write(code)	
	quit method.%Save()
]]></Implementation>
</Method>

<Method name="generatePropertyGetStatic">
<ClassMethod>1</ClassMethod>
<FormalSpec>class:%Dictionary.ClassDefinition,property:%Dictionary.PropertyDefinition</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#dim sc As %Status = $$$OK
	
	#dim method As %Dictionary.MethodDefinition = ..getPropertyMethod(class, property, "GetStatic")
	
	set method.ClassMethod = $$$YES
	set method.Description = property.Name _ " getter method in SQL context"
	set method.FormalSpec = "id:%Integer"
	set method.ReturnType = property.Type
	
	set code = ##class(Temporal.GeneratorsProperty).getCode("GetStatic", property.Name, ..getGlobal(class, property))
	do method.Implementation.Write(code)	
	quit method.%Save()
]]></Implementation>
</Method>

<Method name="generatePropertySet">
<ClassMethod>1</ClassMethod>
<FormalSpec>class:%Dictionary.ClassDefinition,property:%Dictionary.PropertyDefinition</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#dim sc As %Status = $$$OK
	
	#dim method As %Dictionary.MethodDefinition = ..getPropertyMethod(class, property, "Set")
	
	set method.ClassMethod = $$$NO
	set method.Description = property.Name _ " setter method in object context"
	set method.ReturnType = "%Status"
	set method.FormalSpec = "val:" _ property.Type
	
	set code = ##class(Temporal.GeneratorsProperty).getCode("Set", property.Name, ..getGlobal(class, property))
	do method.Implementation.Write(code)
	quit method.%Save()
]]></Implementation>
</Method>

<Method name="setPropertyObject">
<ClassMethod>1</ClassMethod>
<FormalSpec>class:%Dictionary.ClassDefinition,property:%Dictionary.PropertyDefinition</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	/// Enforce:
	/// [ SqlComputeCode = {set {*} = ##class(class).propertyGetStatic({ID})}, SqlComputed, Transient ];

	set property.Transient = $$$YES
	set property.SqlComputed = $$$YES
	set property.SqlComputeCode = "set {*} = ##class(" _class.Name _ ")." _ property.Name _ "GetStatic({ID})"

	quit $$$OK
]]></Implementation>
</Method>

<Method name="generateClassMethods">
<ClassMethod>1</ClassMethod>
<FormalSpec>class:%Dictionary.ClassDefinition</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	#dim sc As %Status = $$$OK
	/// Method %OnAfterSave(insert As %Boolean) As %Status [ Private, ServerOnly = 1 ]
	#dim method As %Dictionary.MethodDefinition = ..getMethod(class, "%OnAfterSave")

	set method.ClassMethod = $$$NO
	set method.Description = "Execute after each save to store temporal data"
	set method.ReturnType = "%Status"
	set method.FormalSpec = "insert:%Boolean"
	set method.Private = $$$YES
	set method.ServerOnly = $$$YES
	
	do method.Implementation.Write($$$TAB _ "set ts = ..ts2unixtime()" _ $$$NL _ $$$TAB _ "set id = ..%Id()")
	
	for i=1:1:class.Properties.Count() {
		#dim property As %Dictionary.PropertyDefinition
		set property = class.Properties.GetAt(i)
		CONTINUE:property.Parameters.GetAt("TIMEPROPERTY")'=$$$YES // Property is not a time roperty
		set code = ##class(Temporal.GeneratorsProperty).getSaveCode(property.Name, ..getGlobal(class, property), property.Type)
		do method.Implementation.Write(code)
	}
	do method.Implementation.Write($$$TAB _ "quit $$$OK")
	quit method.%Save()
]]></Implementation>
</Method>

<Method name="getPropertyMethod">
<ClassMethod>1</ClassMethod>
<FormalSpec>class:%Dictionary.ClassDefinition,property:%Dictionary.PropertyDefinition,methodType:%String(VALUELIST=",Get,GetStatic,Set")</FormalSpec>
<ReturnType>%Dictionary.MethodDefinition</ReturnType>
<Implementation><![CDATA[
	set methodName = property.Name _ methodType
	quit ..getMethod(class, methodName)
]]></Implementation>
</Method>

<Method name="getMethod">
<ClassMethod>1</ClassMethod>
<FormalSpec>class:%Dictionary.ClassDefinition,methodName:%String</FormalSpec>
<ReturnType>%Dictionary.MethodDefinition</ReturnType>
<Implementation><![CDATA[
	
	if ##class(%Dictionary.MethodDefinition).IDKEYExists(class.Name, methodName, .methodId) {
		set method = ##class(%Dictionary.MethodDefinition).%OpenId(methodId)
	} else {
		set method = ##class(%Dictionary.MethodDefinition).%New(class.Name _ ":" _ methodName)
	}
	quit method
]]></Implementation>
</Method>

<Method name="getGlobal">
<ClassMethod>1</ClassMethod>
<CodeMode>expression</CodeMode>
<FormalSpec>class:%Dictionary.ClassDefinition,property:%Dictionary.PropertyDefinition</FormalSpec>
<Implementation><![CDATA[class.Storages.GetAt(1).DataLocation _ property.Name
]]></Implementation>
</Method>
</Class>
</Export>
